buildscript {
    repositories {
        mavenCentral()
        maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.0"
    }
}

apply plugin: 'java-library'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'maven'
apply from: 'publish.gradle'

configure(subprojects) {
    apply plugin: 'java-library'
    apply plugin: 'com.bmuschko.nexus'
    apply from: '../publish.gradle'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    compileJava {
        options.incremental = true
    }
}

def projectName = 'colorful-parent'
modifyPom{
    project{
        name = projectName
    }
}
version '0.5.2-SNAPSHOT'
group 'com.github.tommyettinger'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

subprojects {
    version = "$parent.version"
    group = parent.group
    ext.projectName = 'colorful'
    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }
}

// Disable JDK 8's doclint
// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            // The -quiet is because of some sort of weird JDK JavaCompiler bug:
            // https://discuss.gradle.org/t/passing-arguments-to-compiler-and-javadoc/1661
            options.addStringOption('Xdoclint:none,-missing', '-quiet')
            options.tags('apiNote:a:"API Note"', 'implSpec:a:"Implementation Requirements"', 'implNote:a:"Implementation Note"')
        }
    }
}
repositories {
    mavenCentral()
}
